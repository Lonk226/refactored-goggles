extends CharacterBody2D

@onready var normal_sprite: AnimatedSprite2D = $NormalSprite2D
@onready var shoot_sprite: AnimatedSprite2D = $ShootSprite2D
@onready var collider: CollisionShape2D = $CollisionShape2D
@onready var ladderdown: CollisionShape2D = $LadderDown/CollisionShape2D
@export var walk_speed = 82.5
@export var air_speed = 78.75
@export var ladder_speed = 50
@export var jump_height: float = 50 # height of jump
@export var jump_time_to_peak: float = 0.333 # length of jump
@export var jump_time_to_descent: float = 0.333 # length of fall
var frozen = false
var inching = false
var underwater = false
var shooting = false
var on_ladder = false
var can_go_on_ladder = false
var up_state = false

func _ready() -> void:
	ladderdown.disabled = true

func _physics_process(delta: float) -> void:
	if not frozen:
		var jump_speed: float = ((2 * jump_height) / jump_time_to_peak) * -1 # speed of jump
		var jump_gravity: float = ((-2 * jump_height) / (jump_time_to_peak * jump_time_to_peak)) * -1 # gravity while jumping
		var fall_gravity: float = ((-2 * jump_height) / (jump_time_to_descent * jump_time_to_descent)) * -1 # gravity while falling
		gravity(delta, jump_gravity, fall_gravity)
		jump(jump_speed)
		move()
		shoot()
		climb()
		get_on_ladder()
		up_on_ladder()
		jumpcut()
		inch()
		go_down_on_ladder()
		underwater_physics()
		flip_the_sprite()
		play_animation()
		change_sprite()
		move_and_slide()

func move():
	var dir := Input.get_axis("Left", "Right")
	if not on_ladder:
		if dir:
			if is_on_floor():
				velocity.x = dir * walk_speed
			else:
				velocity.x = dir * air_speed 
		else:
			velocity.x = move_toward(velocity.x, 0, walk_speed)

func jump(jump_speed):
	if Input.is_action_just_pressed("A") and is_on_floor():
		velocity.y += jump_speed
	if Input.is_action_just_pressed("A") and on_ladder:
		on_ladder = false

func play_animation():
	var dir := Input.get_axis("Left", "Right")
	if not is_on_floor():
		if on_ladder:
			if up_state:
				normal_sprite.play("Up")
				shoot_sprite.play("Up")
			if velocity.y == 0:
				normal_sprite.pause()
				shoot_sprite.pause()
			else:
				if not up_state:
					normal_sprite.play("Ladder")
					shoot_sprite.play("Ladder")
		else:
			normal_sprite.play("Jump")
			shoot_sprite.play("Jump")
	elif dir:
		if inching:
			normal_sprite.play("Inch")
			shoot_sprite.play("Idle")
		else:
			normal_sprite.play("Walk")
			shoot_sprite.play("Walk")
	else:
		normal_sprite.play("Idle")
		shoot_sprite.play("Idle")

func flip_the_sprite():
	var dir := Input.get_axis("Left", "Right")
	if dir == 1:
		normal_sprite.flip_h = false
		shoot_sprite.flip_h = false
		collider.position.x = 0.5
	if dir == -1:
		normal_sprite.flip_h = true
		shoot_sprite.flip_h = true
		collider.position.x = -0.5

func gravity(delta, jump_gravity, fall_gravity):
	if not on_ladder:
		if not is_on_floor():
			velocity.y += getgravity(jump_gravity, fall_gravity) * delta

func jumpcut():
	if Input.is_action_just_released("A"):
		if velocity.y < -60:
			velocity.y = -60

func getgravity(jump_gravity, fall_gravity): # tell what the current gravity should be
	if velocity.y < 0:
		return jump_gravity
	else:
		return fall_gravity

func inch():
	if Input.is_action_just_pressed("Left") or Input.is_action_just_pressed("Right"):
		inching = true
		await get_tree().create_timer(0.08).timeout
		inching = false

func underwater_physics():
	if underwater:
		jump_height = 132
		jump_time_to_peak = 1
		jump_time_to_descent = 1
	else:
		jump_height = 50
		jump_time_to_peak = 0.333
		jump_time_to_descent = 0.333

func shoot():
	if Input.is_action_just_pressed("B"):
		shooting = true
		await get_tree().create_timer(0.6).timeout
		shooting = false

func change_sprite():
	if shooting:
		normal_sprite.visible = false
		shoot_sprite.visible = true
	else:
		normal_sprite.visible = true
		shoot_sprite.visible = false

func climb():
	var updir = Input.get_axis("Up","Down")
	if on_ladder:
		velocity.x = 0
		if updir and not shooting:
			velocity.y = updir * ladder_speed
		else:
			velocity.y = move_toward(velocity.y, 0, ladder_speed)
	if is_on_floor():
		on_ladder = false
	if $RayCast2D.is_colliding() and is_on_floor() and updir == -1:
		position.y += -2
	if not $RayCast2D.is_colliding():
		on_ladder = false

func get_on_ladder():
	var updir = Input.get_axis("Up","Down")
	if $RayCast2D.is_colliding():
		if updir:
			on_ladder = true
			global_position.x = $RayCast2D.get_collider().global_position.x

func up_on_ladder():
	if not $RayCast2D2.is_colliding() and on_ladder:
		up_state = true
		position.y -= 10
	else:
		up_state = false

func go_down_on_ladder():
	if Input.is_action_pressed("Down"):
		ladderdown.disabled = false
	else:
		ladderdown.disabled = true
